apply plugin: 'com.android.application'

android {
	compileSdk rootProject.ext.CompileSdk

	namespace 'website.leifs.delta'

	defaultConfig {
		applicationId "website.leifs.delta"
		minSdkVersion rootProject.ext.MinSdk
		targetSdkVersion rootProject.ext.TargetSdk
		versionCode 190003
		versionName "1.9.0-beta3"
		multiDexEnabled true

		// Naming app bundles
		setArchivesBaseName("${parent.name}-v${versionCode}")
	}

	bundle {
		language {
			enableSplit false
		}
	}

	buildTypes {
		release {
			debuggable false
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}
	flavorDimensions "variant"
	productFlavors {
		play {
			dimension "variant"
			resValue "string", "app_name", "Delta"
			matchingFallbacks = ["play"]
		}
		foss {
			dimension "variant"
			applicationIdSuffix '.foss'
			resValue "string", "app_name", "Delta"
			matchingFallbacks = ["foss"]
		}
	}

	sourceSets {
		play { java.srcDirs = ['src/play/java/'] }
		foss { java.srcDirs = ['src/foss/java/'] }

	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_11
		targetCompatibility JavaVersion.VERSION_11
	}
}

dependencies {
	/*
     * CandyBar Material Dashboard Library
     *
     * TODO: How to update the dashboard?!
     * Check this update guide for every version
     *      - https://github.com/zixpo/candybar-sample/wiki/Updating-the-dashboard
     * If you've missed some versions you've to also follow update guide for those versions
     */

	playImplementation 'com.github.zixpo:candybar:3.19.0'
	fossImplementation 'com.github.donnnno:candybar-foss:3.19.0'
}
